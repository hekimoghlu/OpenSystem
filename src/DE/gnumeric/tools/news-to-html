#!/usr/bin/perl -w
# -----------------------------------------------------------------------------

# Note: this isn't perfect, but a very fine start.

my $version = $ARGV[0];
die "$0: must specify version number as argument.\n" unless $version;

my $version2 = $version;
$version2 =~ s/^(\d+\.\d+)\..*/$1/;

my $gnumeric_bug_prefix = "https://gitlab.gnome.org/GNOME/gnumeric/issues/";
my $goffice_bug_prefix = "https://gitlab.gnome.org/GNOME/goffice/issues/";
my $libgsf_bug_prefix = "https://gitlab.gnome.org/GNOME/libgsf/issues/";
my $generic_bug_prefix = "https://gitlab.gnome.org/GNOME";
my $bug_prefix_old = "https://bugzilla.gnome.org/show_bug.cgi?id=";
my $debian_bug_prefix = "http://bugs.debian.org/cgi-bin/bugreport.cgi?bug=";
my $redhat_bug_prefix = "https://bugzilla.redhat.com/show_bug.cgi?id=";
my $ubuntu_bug_prefix = "https://bugs.launchpad.net/ubuntu/+bug/";

my $state = 0;
my $who = undef;
my $item = '';
my $have_item = 0;
my $bug = undef;
my $module = undef;

open (FIL, "<NEWS") or die "$0: cannot open NEWS: $!\n";
while (<FIL>) {
    chomp;

    if (/^(Gnumeric|goffice|libgsf)\s*([^ :]*)\s*:?\s*$/i) {
	if ($2 eq $version) {
	    $state = 1;
	    $module = $1;

	    &prolog ();
	}
	next;
    }

    if (/^-+$/) {
	$state = 0;
	next;
    }

    if ($state == 1) {
	if (/^(.*\S)\s*:\s*$/) {
	    &finish_author () if $who;
	    $who = $1;
	    $have_item = 0;
	    next;
	}

	if (s/^\s+\*\s*//) {
	    &finish_item () if $have_item;
	    $item .= "\t    <li>";
	    &add_to_item ($_);
	    next;
	}

	if (m{^\s+http://bugzilla\.gnome\.org/show_bug\.cgi\?id=(\d+)\s*}) {
	    my $this_bug = $1;
	    &finish_item () if $have_item;
	    $bug = $this_bug;
	    next;
	}

	if ($have_item && s/^\s+//) {
	    &add_to_item ($_);
	    next;
	}
    }
}
close (FIL);
&finish_author () if $who;

&epilog ();

# -----------------------------------------------------------------------------

sub finish_author {
    &finish_item () if $have_item;

    print "        <li>$who\n";
    print "          <ul>\n";
    print $item;
    print "          </ul>\n";
    print "        </li>\n";
    $item = '';
    $who = undef;
}

sub finish_item {
    if ($item && $item !~ /[.?!]\s*$/) {
	$item .= ".";
    }
    if ($bug) {
	$item .= " (<a href=\"$bug_prefix_old$bug\">Bug $bug</a>.)";
	$bug = '';
    }
    $item =~ s/\s+$//;
    $item .= "\n\t    </li>\n";
    $have_item = 0;
}

sub add_to_item {
    my ($txt) = @_;

    $txt =~ s{Debian\s+\#(\d\d\d\d\d+)\b}{Debian <a href=\"$debian_bug_prefix$1\"\>Bug $1</a>}g;
    $txt =~ s{Redhat\s+\#(\d\d\d\d\d+)\b}{Redhat <a href=\"$redhat_bug_prefix$1\"\>Bug $1</a>}g;
    $txt =~ s{Ubuntu\s+\#(\d\d\d\d\d+)\b}{Ubuntu <a href=\"$ubuntu_bug_prefix$1\"\>Bug $1</a>}g;
    $txt =~ s{Gnumeric \#(\d\d\d+)\b}{<a href=\"$gnumeric_bug_prefix$1\"\>Bug $1</a>}gi;
    $txt =~ s{Goffice \#(\d\d\d+)\b}{<a href=\"$goffice_bug_prefix$1\"\>Bug $1</a>}gi;
    $txt =~ s{Libgsf \#(\d\d\d+)\b}{<a href=\"$libgsf_bug_prefix$1\"\>Bug $1</a>}gi;

    $txt =~ s{\#(\d\d\d\d\d+)}{<a href=\"$bug_prefix_old$1\"\>Bug $1</a>}g;

    if (m{\#(\d\d\d+)} && $1 > 300) {
	# Assume Gnumeric bug number
	$txt =~ s{\#(\d\d\d+)\b}{<a href=\"$gnumeric_bug_prefix$1\"\>Bug $1</a>}gi;
    } else {
	$txt =~ s{\#(\d\d+)\b}{<a href=\"$generic_bug_prefix/$module/issues/$1\"\>Bug $1</a>}g;
    }

    $item .= "\n\t      $txt";
    $have_item = 1;
}

# -----------------------------------------------------------------------------

sub prolog {
    print <<PROLOG;
<?xml version="1.0" encoding="UTF-8"?>
<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
<!-- MARKER: start-header -->
<head>
<title>Gnumeric</title>
<link rel="stylesheet" href="/style/style.css" type="text/css" />
<link rel="icon" type="image/png" href="/logo.png" />
</head>
<body>
<div id="wrap">
  <a href="/"><div id="header">
    <h1 id="logo-text"><span>Gnumeric</span></h1>
    <p id="slogan">Free, Fast, Accurate &mdash; Pick Any Three!</p>
    <img id="logo" src="/gnumeric.png" alt="logo" class="float-right"/>
    </div></a>

  <div id="nav">
    <ul>
      <li><a href="/">Home</a></li>
      <li><a href="/development.html">Development</a></li>
      <li><a href="/contact.html">Contact</a></li>
    </ul>
  </div>

  <div id="content-wrap">
    <!-- MARKER: start-main -->
    <div id="fullmain">
      <div class="generalitem">
PROLOG
;
    print "        <h2><span class=\"gnumeric-bullet\"></span>$module $version aka &quot;TBD&quot; is now available.</h2>\n";
    print "        <p>The Gnumeric Team is pleased to announce the availability of\n";
    print "        $module version $version.</p>\n";
    print "        <p>Noteworthy news in this release:</p>\n";
    print "        <ul>\n";
    print "        <li>...</li>\n";
    print "        </ul>\n\n";
    print "        <p>Details of changes by contributor:</p>\n";
    print "        <ul>\n";
}

# -----------------------------------------------------------------------------

sub epilog {
    my $link = "https://ftp.gnome.org/pub/GNOME/sources/" . lc $module . "/$version2/";

    print "        </ul>\n";

    print "      </div>\n";
    print "      <div class=\"generalitem\">\n";
    print "        <h2><span class=\"gnumeric-bullet\"></span>Availability</h2>\n";
    print "        <blockquote>\n";
    print "          <p><a href=\"$link\">$link</a></p>\n";
    print "        </blockquote>\n";
    print "      </div>\n";
    print "    </div>\n";
    print "    <!-- MARKER: end-main -->\n";
    print "    <!-- MARKER: start-sidebar -->\n";
    print "    <!-- MARKER: end-sidebar -->\n";
    print "  </div>\n";
    print "</div>\n";
    print "</body>\n";
    print "</html>\n";
}

# -----------------------------------------------------------------------------
