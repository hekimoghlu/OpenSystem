#!/usr/bin/perl -w
#
#  Gnumeric
#
#  Copyright (C) 2001,2014 Morten Welinder.
#
#  This program is free software; you can redistribute it and/or
#  modify it under the terms of the GNU General Public License as
#  published by the Free Software Foundation; either version 2 of the
#  License, or (at your option) any later version.
#
#  This library is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
#  General Public License for more details.
#
#  You should have received a copy of the GNU General Public License
#  along with this library; if not, see <https://www.gnu.org/licenses/>.
#
#  Author: Morten Welinder <terra@gnome.org>

use strict;

my $exitcode = 0;

die "$0: must be run from top-level directory.\n"
    unless (-r "configure.ac" &&
	    -r 'ChangeLog' &&
	    -r 'po/POTFILES.in');

my %exceptions =
    (
     );


my @pot_files;
my %pot_files;
foreach my $pofilename ("po/POTFILES.in", "po-functions/POTFILES.in") {
    local (*POT);
    open (*POT, "<$pofilename") or die "Cannot read $pofilename: $!\n";
    while (<POT>) {
	next if /^\s*\#/ || /^\s*$/;
	next if /^\s*\[encoding/;

	s{^\s*\[type: gettext/glade\]}{};

	chomp;
	if(/^\s+/) {
		print STDERR "WARNING: leading whitespace detected in line ($_)\n";
		s/^\s+//;
	}
	if(/\s+$/) {
		print STDERR "WARNING: trailing whitespace detected in line ($_)\n";
		s/\s+$//;
	}
	push @pot_files, $_;
	$pot_files{$_} = 1;
    }
}

foreach my $filename (@pot_files) {
    next if -r $filename && -f $filename;

    print STDERR "$0: File `$filename' listed for translation isn't there.\n";
    $exitcode = 1;
}

{
    my $first_unknown = 1;

    local (*GIT);
    open (*GIT, "git status --porcelain 2>&1 |")
	or die "$0: cannot execute git: $!\n";
    while (<GIT>) {
	chomp;

	if (/^\?+\s+(.*)$/) {
	    print STDERR "$0: File `$1' is not known.\n";
	    if ($first_unknown) {
		$first_unknown = 0;
		print STDERR "$0:   Suggest delete, git add, or .gitignore solution.\n";
	    }
	    $exitcode = 1;
	    next;
	}

	if (/^\s*A\s*(.*)$/) {
	    print STDERR "$0: New file `$1' has not been committed.\n";
	    $exitcode = 1;
	    next;
	}

	if (/^\s*M\s*(.*)$/) {
	    print STDERR "$0: Modified file `$1' has not been committed.\n";
	    $exitcode = 1;
	    next;
	}

	if (/^\s*D\s*(.*)$/) {
	    print STDERR "$0: Deletion of file `$1' has not been committed.\n";
	    if ($pot_files{$1}) {
		print STDERR "$0: FYI, that file is listed for translation.\n";
	    }
	    $exitcode = 1;
	    next;
	}
    }
}

exit $exitcode;
